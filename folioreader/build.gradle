apply plugin: 'com.android.library'
apply from: '../config/quality/quality.gradle'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'folioreader'

    publishedGroupId = 'com.folioreader'
    libraryName = 'FolioReader'
    artifact = 'folioreader'

    ANDROID_LIB_VERSION = '27.1.1'
    R2_STREAMER_VERSION = '0.1.3'

    libraryDescription = 'An epub reader for Android'

    siteUrl = 'https://github.com/FolioReader/FolioReader-Android'
    gitUrl = 'https://github.com/FolioReader/FolioReader-Android.git'

    libraryVersion = '0.3.3'

    developerId = 'mobisystech'
    developerName = 'Folio Reader'
    developerEmail = 'mahavir@codetoart.com'

    licenseName = 'FreeBSD License'
    licenseUrl = 'https://en.wikipedia.org/wiki/FreeBSD_Documentation_License#License'
    allLicenses = ["FreeBSD"]
}

android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        versionCode 1
        versionName "1.0"
        minSdkVersion 16
        targetSdkVersion 27
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['res']
        }
        test {
            java.srcDirs = ['src/test/java']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        abortOnError false
    }

    checkstyle {
        ignoreFailures = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':webViewMarker')

    //noinspection GradleDependency
    implementation 'com.daimajia.swipelayout:library:1.2.0@aar'
    implementation "com.android.support:design:$project.ANDROID_LIB_VERSION"
    implementation "com.android.support:support-v4:$project.ANDROID_LIB_VERSION"
    implementation "com.android.support:appcompat-v7:$project.ANDROID_LIB_VERSION"
    implementation "com.android.support:recyclerview-v7:$project.ANDROID_LIB_VERSION"

    // r2-streamer
    implementation "org.readium:r2-parser:$project.R2_STREAMER_VERSION"
    implementation "org.readium:r2-server:$project.R2_STREAMER_VERSION"
    implementation "org.readium:r2-fetcher:$project.R2_STREAMER_VERSION"
    implementation 'org.greenrobot:eventbus:3.1.1'
}

apply from: '../folioreader/bintray/installv1.gradle'
apply from: '../folioreader/bintray/bintrayv1.gradle'
